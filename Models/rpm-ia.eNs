create model ia

#LAYERS AND CONNECTIONS
create string_layer in_layer
create iac_layer bigrams
create iac_layer words

create connection ibc
create sparse_fan_connection bwc
create connection wbc
create connection lato

#DATAFRAME
#this will create column_rep representations for each column
create dataframe_for_reps bigramf
bigramf load Databases/bigrams_lowercase.eNd
create dataframe_for_reps vocab
vocab load Databases/Vocab_files/ew4.eNd
#vocab load Databases/Vocab_files/ia_8LW.eNd

#REPRESENTATIONS
create string_representation str_rep
create string_representation bg_str_rep

#ASSIGN LAYER REPRESENTATIONS
in_layer represent str_rep
bigrams represent (bigramf column_rep bigram keyed_representation)
words represent (vocab column_rep Word keyed_representation)
words set_resting_level (vocab column_rep Rest)

#CONVERSIONS
#1 - from str_rep to bg_str_rep
create regex_side_effect_conversion list_OBs
list_OBs set_source str_rep
list_OBs set_target bg_str_rep
list_OBs add ^(.)(.)(.)(.)(.)(.)(.)(.) \2\3\4\5\6\7\8 \1\2\1\3\1\4
list_OBs add ^(.)(.)(.)(.)(.)(.)(.) \2\3\4\5\6\7 \1\2\1\3\1\4
list_OBs add ^(.)(.)(.)(.)(.)(.) \2\3\4\5\6 \1\2\1\3\1\4
list_OBs add ^(.)(.)(.)(.)(.) \2\3\4\5 \1\2\1\3\1\4
list_OBs add ^(.)(.)(.)(.) \2\3\4 \1\2\1\3\1\4
list_OBs add ^(.)(.)(.) \2\3 \1\2\1\3
list_OBs add ^(.)(.) \2 \1\2

#2 - from  bg_str_rep to (bgsplit get_target)
create string_split bgsplit
bgsplit set_source bg_str_rep
bgsplit set_chunk 2

#3 - from (bgsplit get_target) to (bigramf column_rep bigram keyed_representation)
#create key_conversion bgkeyc
#bgkeyc set_target (bigramf column_rep bigram keyed_representation)
#bgkeyc set_source (bgsplit get_target)

create equiv_conversion bg1tobg2
bg1tobg2 set_target (bigramf column_rep bigram string_representation)
bg1tobg2 set_source (bgsplit get_target)


#4 - from(vocab column_rep Word string_representation) to (str_rep )
create equiv_conversion wordtostrc
wordtostrc set_target str_rep 
wordtostrc set_source (vocab column_rep Word string_representation)

#CONNECT LAYERS
ibc autoconnect in_layer bigrams
#bwc autoconnect bigrams words
#wbc autoconnect  words bigrams
lato autoconnect words words

bwc set_autoconnect 0
bwc attach bigrams
words attach bwc 
bwc conv_fill ((bigramf column_rep bigram) find_conversion (vocab column_rep Word))

wbc autoconnect  words bigrams


#OTHER STUFF
ia define_input orthographic str_rep in_layer
include Trials/Presentation_No_Task.eNs
(ia parameters) load Databases/Parameter_Sets/RPM-IA/rpm-ia.eNp
